// Shared modules build

version = '0.2.2-SNAPSHOT'
def repositoryName = "xbup-java"
def githubProfile = 'exbin'
def libraryUrl = 'https://xbup.exbin.org'

ext.depsThis = 'XbupJava'

ext.depsXbupJava = [
    githubProfile: githubProfile,
    githubRepo   : repositoryName,
    version      : version
]

ext.depsExbinAuxiliaryJava = [
    githubProfile: 'exbin',
    githubRepo   : 'exbin-auxiliary-java',
    version      : '0.2.1'
]

import org.gradle.util.VersionNumber

apply from: '../deps_processing.gradle'

sourceCompatibility = '1.8'

targetCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    doFirst {
        options.fork = true
        if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
            if (VersionNumber.parse(gradle.gradleVersion) >= VersionNumber.parse('5.0')) {
                options.bootstrapClasspath = files("${System.properties['java.home']}/lib/rt.jar", "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar")
            } else {
                options.bootClasspath = "${System.properties['java.home']}/lib/rt.jar"
                options.bootClasspath += "$File.pathSeparator${System.properties['java.home']}/jre/lib/jsse.jar"
            }
        }
    }
}

// Disable tests on build
if (!gradle.startParameter.taskNames.any { it.endsWith("test") }) {
    tasks.withType(Test) { enabled = false }
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = 'tests'
    archiveBaseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    tests testJar
}

task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.java
}

javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
        options.addBooleanOption('html5', true)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar {
                archiveClassifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name libraryName
                    description libraryDescription
                    url libraryUrl
                    scm {
                        url "https://github.com/${githubProfile}/${repositoryName}"
                        connection "cm:git:git://github.com/${githubProfile}/${repositoryName}.git"
                        developerConnection "scm:git:git@github.com:${githubProfile}/${repositoryName}.git"
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'HajdaM'
                            name 'Miroslav Hajda'
                            email 'hajdam@users.sf.net'
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

if (gradle.startParameter.taskNames.contains("uploadArchives") && !version.endsWith('-SNAPSHOT')) {
    artifacts {
        archives javadocJar, sourceJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
//                beforeDeployment {
//                    MavenDeployment deployment -> signing.signPom(deployment)
//                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name libraryName
                    packaging 'jar'
                    description libraryDescription
                    url libraryUrl

                    scm {
                        url "https://github.com/${githubProfile}/${repositoryName}"
                        connection "cm:git:git://github.com/${githubProfile}/${repositoryName}.git"
                        developerConnection "scm:git:git@github.com:${githubProfile}/${repositoryName}.git"
                    }
                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'HajdaM'
                            name 'Miroslav Hajda'
                            email 'hajdam@users.sf.net'
                        }
                    }
                }
            }
        }
    }
}
