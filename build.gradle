buildscript {
    repositories {
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'base'
    id 'distribution'
    id 'maven-publish'
    id "com.netflix.nebula.ospackage" version "11.11.2"
}

apply plugin: 'com.netflix.nebula.ospackage-base'
apply plugin: 'com.netflix.nebula.rpm'

version = '0.2.5-SNAPSHOT'

task cleanDeps {
    if (gradle.startParameter.taskNames.contains("clean") && !project.gradle.startParameter.projectProperties.containsKey("moduleDep")) {
        println "Deleting temporary dependency modules"
        File depsCheckFile = rootProject.file("deps/dependency.txt")
        if (depsCheckFile.exists()) {
            File depsDir = rootProject.file("deps")
            depsDir.listFiles().each() { childFile ->
                if ((childFile.isDirectory() && childFile.name != ".gradle") || childFile.name.endsWith(".zip")) {
                    project.delete(childFile)
                }
            }
        } else {
            throw new Exception('Invalid dependency path')
        }
    }
}

clean.dependsOn cleanDeps

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

subprojects {
    task allDeps(type: DependencyReportTask) {}
}

if (file('deps/.downloaded').exists()) {
    file('deps/.downloaded').delete()
    // Performs another build task for new dependency handling
    def depsLevel
    if (project.gradle.startParameter.projectProperties.containsKey('moduleDep')) {
        depsLevel = project.gradle.startParameter.projectProperties['moduleDep'] + 1
    } else {
        depsLevel = 1
        println "Due to dependecy download(s) gradle build must be executed again after finishing"
    }
    def artefactBuild = project.tasks.create([name: "depsRebuild_${depsLevel}", type: GradleBuild])
    artefactBuild.tasks = project.gradle.startParameter.taskNames
    artefactBuild.setDependsOn(project.tasks['assemble'].dependsOn)

    def artefactProjectProperties = artefactBuild.startParameter.projectProperties
    def currentProjectProperties = project.gradle.startParameter.projectProperties
    artefactProjectProperties << currentProjectProperties
    artefactProjectProperties << ['moduleDep': depsLevel]
    
    // Terminate currently invalid tasks
    // TODO: Fix support for other tasks like distZip
    subprojects.each { subproject ->
        if (subproject.tasks.findByPath('compileJava') != null) {
            subproject.tasks['compileJava'].setEnabled(false)
        }
        if (subproject.tasks.findByPath('compileTestJava') != null) {
            subproject.tasks['compileTestJava'].setEnabled(false)
        }
        if (subproject.tasks.findByPath('javadoc') != null) {
            subproject.tasks['javadoc'].setEnabled(false)
        }
    }

    assemble.dependsOn("depsRebuild_${depsLevel}")
}

distTar.dependsOn project(':apps:bined-editor').tasks['createExe']
distZip.dependsOn project(':apps:bined-editor').tasks['createExe']

ospackage {
    packageName = 'bined'
    if (project.version.endsWith("-SNAPSHOT")) {
        version = project.version.substring(0, project.version.length() - 9)
        release = '0'
        // '0.' + new Date().format('yyyyMMdd') + "dev"
    } else {
        version = project.version
        release = '1'
    }
    arch = NOARCH
    os = LINUX
    packageGroup = 'Converted/utils'
    license = 'Apache V2'
    vendor = 'ExBin Project'
    description = 'Free and open source binary/hex viewer/editor and component written in Java.'
    distribution = 'Linux'
    url = 'https://bined.exbin.org'
    user = 'root'

    from ('src/ospackage/bined') {
        into '/usr/bin'
    }

    from ('src/ospackage/bined.desktop') {
        into '/usr/share/applications'
    }

    from ('src/ospackage/bined.png') {
        addParentDirs = false
        into '/usr/share/pixmaps'
    }

    from (project(':apps:bined-editor').jar) {
        rename { filename -> "bined.jar" }
        into '/usr/share/bined'
    }

    from ('src/dist/readme.txt') {
        into '/usr/share/bined'
    }

    from ('src/dist/plugins') {
        into '/usr/share/bined/plugins'
    }

    from ('apps/bined-editor/help') {
        into 'usr/share/bined/help'
    }

    project(':apps:bined-editor').plugins.withType(JavaPlugin) {
        from(project(':apps:bined-editor').configurations.runtimeClasspath) {
            into('/usr/share/bined/lib')
            include '*'
        }
    }
}

// Breaks for Java 8+ and in some Gradle versions :-/
if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
    buildRpm {
        from ('src/ospackage/bined.png') {
            // Doesn't propagate correctly from ospackage task, so repeat it here
            addParentDirs = false
            into '/usr/share/pixmaps'
        }

        requires('jre', '1.8.0', GREATER | EQUAL)
    }

    buildDeb {
        arch = 'all'
        requires('default-jre', '1.8.0', GREATER | EQUAL)
    }
}

task("copyDependencies", type: Copy) {
    from(project(':apps:bined-editor').configurations.runtimeClasspath).into("$buildDir/jars")
}

task("copyJar", type: Copy) {
    from(project(':apps:bined-editor').jar).into("$buildDir/jars")
}

if (!project.gradle.startParameter.projectProperties.containsKey("moduleDep")) {
    distZip {
        archiveFileName = 'bined-' + project.version + '.zip'
    }

    distributions {
        main {
            contents {
                duplicatesStrategy = 'exclude'
                from project(':apps:bined-editor').jar

                from project.rootDir
                include 'LICENSE.txt'
                include 'changes.txt'

                from ('src/dist') {
                    include 'readme.txt'
                    include 'bined.sh'
                //                include 'bined.bat'
                    include 'plugins/*'
                }

                into('dist/images') {
                    from ('src/ospackage') {
                        include 'bined.png'
                        include 'bined.svg'
                    }
                }
                
                into('dist/linux') {
                    from ('src/ospackage') {
                        include 'bined.desktop'
                    }
                }

                into('dist/flathub') {
                    from ('src/flathub') {
                        include 'org.exbin.BinEd'
                        include 'org.exbin.BinEd.desktop'
                        include 'org.exbin.BinEd.svg'
                        include 'org.exbin.BinEd.appdata.xml'
                    }
                }

                from project(':apps:bined-editor').projectDir.absolutePath + '/build/launch4j'
                include "Bined.exe"

                //            into('lib') {
                //                from 'lib'
                //            }

                into('') {
                    from project(':apps:bined-editor').jar.archiveFile
                    include '*'
                    rename { filename -> "bined.jar" }
                }

                project(':apps:bined-editor').plugins.withType(JavaPlugin) {
                    into('lib') {
                        from project(':apps:bined-editor').configurations.runtimeClasspath
                        include '*'
                    }
                }

                //            into('doc') {
                //                from 'doc'
                //                include '**'
                //            }
                //            into('resources') {
                //                from 'resources'
                //                include '**'
                //                exclude 'private'
                //            }
            }
        }
    }
}
